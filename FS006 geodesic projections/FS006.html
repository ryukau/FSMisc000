<!DOCTYPE html>
<html>
	<head>
		<meta charset=utf-8>
		<title>FS 006</title>
		<style>
			body { margin: 0; }
			canvas { width: 100%; height: 100% }
		</style>

        <script id="fragmentShader" type="x-shader/x-fragment">
            #define M_PI 3.1415926535897932384626433832795
            #define SQRT_2 1.4142135623730950488016887242096980785696718753769480

			uniform float time;
			uniform vec2 resolution;

			varying vec2 vUv;

            //
            // 便利関数
            //
            vec2 normalizedPosition()
            {
                return -1.0 + 2.0 * vUv; // これはアスペクト比によって歪む。Three.jsではこのほうが便利
                //return (gl_FragCoord.xy * 2.0 - resolution) / min(resolution.x, resolution.y); // フラグメントシェーダ専用シーンでのみ使える
            }

            vec2 rotate(vec2 p, float theta)
            {
                return vec2(
                    p.x * cos(theta) - p.y * sin(theta),
                    p.x * sin(theta) + p.y * cos(theta)
                    );
            }

            float cosh(float val)
            {
                float tmp = exp(val);
                float cosH = (tmp + 1.0 / tmp) / 2.0;
                return cosH;
            }

            float tanh(float val)
            {
                float tmp = exp(val);
                float tanH = (tmp - 1.0 / tmp) / (tmp + 1.0 / tmp);
                return tanH;
            }

            float sinh(float val)
            {
                float tmp = exp(val);
                float sinH = (tmp - 1.0 / tmp) / 2.0;
                return sinH;
            }

            //
            // 素材
            //
            vec2 polar(vec2 p)
            {
                return vec2(p.x * p.x + p.y * p.y, atan(p.x, p.y));
            }

            vec2 equirectangular(vec2 p)
            {
                float phi = time * 0.1; // 地図では定数を使う
                return vec2(p.x, p.y / cos(phi));
            }

            vec2 cassini(vec2 p)
            {
                // http://mathworld.wolfram.com/CassiniProjection.html

                float lambda0 = sin(time);  // 中心となる経度
                float phi0    = time * 0.2; //           緯度

                float D = p.y + phi0;
                return vec2(asin(sin(D) * cos(p.x)), lambda0 + atan(tan(p.x)/cos(D)));
            }

            vec2 mercator(vec2 p)
            {
                //float R = pow(0.9, time * 3.0);
                float R = 1.0;
                float lambda0 = 0.0;//time; // 中心となる経度

                return vec2(lambda0 + p.y / R, 2.0 * atan(exp(p.y / R)) - M_PI / 2.0);
            }

            vec2 transverseMercator(vec2 p)
            {
                // https://en.wikipedia.org/wiki/Transverse_Mercator_projection
                // Inverse transformation formulaeの項

                float k0 = sin(time * 0.3); // sec(中心となる経度)

                float ka = 1.0 / k0;
                float x = p.x * ka;
                float y = p.y * ka;

                return vec2(atan(sinh(x) / cos(y)), asin(sin(y) / cosh(x)));
            }

            vec2 lambert(vec2 p)
            {
                // 円筒への射影

                float lambda0 = time; // 中心経度、ずらしと相性がいい
                return vec2(p.x + lambda0, asin(p.y));
            }

            vec2 gallPeters(vec2 p)
            {
                // 円筒への射影

                float R = 0.7;

                float lambda = p.x * SQRT_2 / R;
                float phi = asin(p.y / (R * SQRT_2));

                return vec2(lambda, phi);
            }

            vec2 centralCylindical(vec2 p)
            {
                // メルカトルに似ている

                float R = 1.0;
                float lambda0 = 0.0;//time; // 中心となる経度

                return vec2(lambda0 + p.y / R, atan(p.y / R));
            }

            vec2 sinusoidal(vec2 p)
            {
                // lambertの縦版

                float lambda0 = time; // 中心経度、ずらしと相性がいい
                return vec2(lambda0 + acos(p.x), p.y);
            }

            vec2 mollweide(vec2 p)
            {
                // 縦縞と相性がいい

                float R = 1.0; // 射影する円筒の半径
                float lambda0 = time; // 中心経度

                float theta = 2.0 * asin(p.y / (R * SQRT_2));
                float phi = asin(theta + sin(theta) / M_PI);
                float lambda = lambda0 + M_PI * p.x / (2.0 * R * SQRT_2 * cos(theta));
                return vec2(lambda, phi);
            }

            vec2 eckertIIForward(vec2 p)
            {
                // eckert II はあまりに奇妙だったので正射影、逆射影の両方を試した。
                // 何らかのミスも考えられる。
                // どちらも横縞と相性が悪い。

                float R = 2.0;//1.2 * sin(time); // ??
                float lambda0 = 0.0; // 中心経度

                float a = sqrt(4.0 - 3.0 * sin(p.y));
                float x = 2.0 * R * (p.x - lambda0) * a / sqrt(6.0 * M_PI);
                float y = R * sqrt(2.0 * M_PI / 3.0) * (2.0 - a);
                return vec2(x, y);
            }

            vec2 eckertIIInverse(vec2 p)
            {
                float R = 0.2 * sin(time); // ??
                float lambda0 = 0.0; // 中心経度

                const float ssp = sqrt(6.0 * M_PI); // ssp: sqrt six pi

                float lambda = lambda0 + (ssp * p.x / SQRT_2) / (2.0 * ssp * R - 3.0 * p.y);
                float phi = asin((4.0 - 24.0 * M_PI) / 3.0 + 4.0 * ssp * p.y / R - 9.0 * p.y * p.y / (3.0 * R * R));
                return vec2(lambda, phi);
            }

            vec2 eckertIV(vec2 p)
            {
                float R = 0.7; // 射影する円筒の半径
                float lambda0 = 0.0; // 中心経度

                float theta = asin(p.y / (1.3265004 * R));
                float phi = asin((theta + sin(theta) * cos(theta) + 2.0 * sin(theta)) / (2.0 + M_PI / 2.0));
                float lambda = lambda0 + p.x / (0.4222382 * R * (1.0 + cos(theta)));

                return vec2(lambda, phi);
            }

            vec2 eckertVI(vec2 p)
            {
                // http://mathworld.wolfram.com/EckertVIProjection.html

                float lambda0 = 0.0; // 中心経度

                const float stpp = sqrt(2.0 + M_PI); // stpp: sqrt two plus pi

                float theta = p.y * stpp * 0.5;
                float phi = asin((theta + sin(theta)) / (1.0 + M_PI * 0.5));
                float lambda = lambda0 + stpp * p.x / (1.0 + cos(theta));

                return vec2(lambda, phi);
            }

            vec2 orteliusOvalForward(vec2 p)
            {
                // 逆射影は場合分けがややこしいので正射影のみ

                float R = 8.0;
                float lambda0 = 0.0;

                float y = R * p.y;
                float L = abs(p.x - lambda0);
                float F = 0.5 * (M_PI * M_PI / (4.0 * L) + L);
                float yr = y / R;
                float x = R * (L - F + sqrt(F * F - yr * yr));

                return vec2(x, y);
            }

            // goodeHomolosine
            //
            // http://edc2.usgs.gov/1KM/goodesarticle.php
            //
            // 場合分けと漸化式が出てきてややこしい
            //

            vec2 kavrayskiyVII(vec2 p)
            {
                float pp = p.y / M_PI; // pp: phi pi, where phi = p.y
                float lambda = p.x * 2.0 / (3.0 * sqrt(0.33333333 - pp * pp));

                return vec2(lambda, p.y);
            }

            // robinson     https://en.wikipedia.org/wiki/Robinson_projection
            // naturalEarth https://en.wikipedia.org/wiki/Natural_Earth_projection
            //
            // 数列テーブルと補間(Aitken interpolation)が必要
            //

            // toblerHyperelliptical
            //
            // https://en.wikipedia.org/wiki/Tobler_hyperelliptical_projection
            //
            // 円筒への射影で長方形になったマップをさらにhyperellipseに射影
            //

            vec2 wagnerVI(vec2 p)
            {
                float pp = p.y / M_PI; // pp: phi pi, where phi = p.y
                float lambda = p.x / sqrt(1.0 - 3.0 * pp * pp);

                return vec2(lambda, p.y);
            }

            vec2 bonne(vec2 p)
            {
                float lambda0 = 0.0;
                float phi1 = M_PI * 0.2;

                float phicot = 1.0 / tan(phi1);
                float yy = phicot - p.y;
                float rho = sqrt(p.x * p.x + yy * yy);
                float phi = phicot + phi1 - rho;
                float lambda = lambda0 + (rho / cos(phi)) * atan(p.x / (phicot - p.y));

                return vec2(lambda, phi); // vec2(0.0, 1.3) の平行移動で面白い模様が見える
            }

            vec2 poincare(vec2 p)
            {
                float a = 1.0;
                return vec2(p.x, sqrt(a * a - (p.x - 0.5)));
            }

            float stripe()
            {
                vec2 p = bonne(normalizedPosition() + vec2(0.0, 1.3));

                return mod(
                    + p.y * 32.0               // 横縞
                    //+ p.x * 32.0               // 縦縞
                    + floor(p.x * 16.0)        // ずらし
                    - time * 30.0
                    , 10.0) < 4.0 ? 0.0 : 1.0; // [0.0, 1.0]の範囲に収める
            }

            //
            // 場面
            //
			void main()
            {
                float l = stripe(); // lightness

				gl_FragColor = vec4( vec3(l), 1.0 );
			}
        </script>

        <script id="vertexShader" type="x-shader/x-fragment">
            varying vec2 vUv;

            void main()
            {
                vUv = uv;
                vec4 mvPosition = modelViewMatrix * vec4( position, 1.0 );
                gl_Position = projectionMatrix * mvPosition;
            }
        </script>
        <script data-main="js/main" src="js/require.js"></script>
	</head>
	<body>
	</body>
</html>